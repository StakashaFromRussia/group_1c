
#Область ОбработчикиЭлементовФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВерсииКонфигурацийИнформационныхБазВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ВерсииКонфигурацийИнформационныхБаз.ТекущиеДанные;
	
	ИмяПоля = Поле.Имя;
	Если ИмяПоля = "ВерсииКонфигурацийИнформационныхБазИнформационнаяБаза" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ИнформационныеБазы.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.ИнформационнаяБаза), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ИмяПоля = "ВерсииКонфигурацийИнформационныхБазКонфигурация" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.Конфигурации.ФормаОбъекта", Новый Структура("Ключ", ТекущиеДанные.Конфигурация), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформационнуюБазу(Команда)
	
	//Не используем больше
	Возврат;
	ТекущиеДанные = Элементы.ВерсииКонфигурацийИнформационныхБаз.ТекущиеДанные;

	ПолноеИмяФайла = КаталогВременныхФайлов() + "Update_base.bat";
	
	МассивВерсий = ВыбратьРелизДляУстановки(ТекущиеДанные.ИнформационнаяБаза);
	
	Для Счетчик = 0 По МассивВерсий.Количество() - 1 Цикл
		Версия = СтрЗаменить(МассивВерсий[МассивВерсий.Количество() - Счетчик - 1], ".", "_");
		
		Если Версия = "" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не удалось выбрать необходимый релиз для установки";
			Сообщение.Сообщить();
			Прервать;
		КонецЕсли;
		ТекстКоманды = ПодготовитьТекстКоманды(ТекущиеДанные.ИнформационнаяБаза, "CONFIG", Версия);
		Если ТекстКоманды = "" Тогда
			Прервать;
		КонецЕсли;
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.ДобавитьСтроку(ТекстКоманды);
		ТекстовыйДокумент.Записать(ПолноеИмяФайла, КодировкаТекста.ANSI);
		ЗапуститьПриложение(ПолноеИмяФайла,, Истина);
		
		ТекстКоманды = ПодготовитьТекстКоманды(ТекущиеДанные.ИнформационнаяБаза, "enterprise", Версия);
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.ДобавитьСтроку(ТекстКоманды);
		ТекстовыйДокумент.Записать(ПолноеИмяФайла, КодировкаТекста.ANSI);
		ЗапуститьПриложение(ПолноеИмяФайла,, Истина);
	КонецЦикла;
	
	//Необходимо обновить информацию о ИБ
	ОбщегоНазначенияВызовСервера.ОбновитьСписокИБНаСервере(ТекущиеДанные.ИнформационнаяБаза);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьТекстКоманды(ИнформационнаяБаза, РежимЗапуска, УстанавливаемаяВерсия)
	
	//Обновлять можем только полностью типовые конфигурации
	//1. Проверяем, стоит ли база на поддержке
	Если НЕ ИнформационнаяБаза.Типовая Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Конфигурация информационной базы снята с полной поддержки, автоматическое обновление невозможно.";
		Сообщение.Сообщить();
		Возврат "";
	КонецЕсли;
	
	ВерсияПлатформы = ОбщегоНазначенияСервер.ПолучитьИспользуемуюВерсиюПлатформы();
	КаталогСОбновлениями = "\\192.168.100.9\Exchange\Update_1c\1c";
	КаталогЛогов = "\\192.168.100.9\Exchange\Update_1c\log";
	РелизОбновления = УстанавливаемаяВерсия;
	КаталогКонфигурации = ИнформационнаяБаза.Конфигурация.НаименованиеКаталогаОбновлений;
	Сервер = ИнформационнаяБаза.Сервер;
	ИмяБазы = ИнформационнаяБаза.ИмяФайла;
	ЛогинПароль = ОбщегоНазначенияВызовСервера.ПолучитьЛогинПарольАдминистратора();
	
	//Сначала собираем файлик
	ТекстФайла = "";
	ТекстФайла = ТекстФайла + "chcp 1251" + Символы.ПС;
	ТекстФайла = ТекстФайла + "setlocal" + Символы.ПС;
	ТекстФайла = ТекстФайла + "set bin_dir=""C:\Program Files\1cv8""" + Символы.ПС;
	ТекстФайла = ТекстФайла + "set bin_ver=" + ВерсияПлатформы + Символы.ПС;
	ТекстФайла = ТекстФайла + "set cfu_dir=" + КаталогСОбновлениями + Символы.ПС;
	ТекстФайла = ТекстФайла + "set log_dir=" + КаталогЛогов + Символы.ПС;
	
	ТекстФайла = ТекстФайла + "set cfu=" + РелизОбновления + Символы.ПС;
	ТекстКоманды = ТекстФайла + "%bin_dir%\%bin_ver%\bin\1cv8.exe " + РежимЗапуска + " /S " + Сервер + "\" + ИмяБазы + " /N" + ЛогинПароль.Логин + " /P" + ЛогинПароль.Пароль + " /UpdateCfg %cfu_dir%\" + КаталогКонфигурации +"\%cfu%\1Cv8.cfu /UpdateDBCfg /Out %log_dir%\Vector_" + РежимЗапуска + "_%cfu%.log" + Символы.ПС;
	
	Возврат ТекстКоманды;
	
КонецФункции

#КонецОбласти

#Область ПрочиеОбработчики

// Процедура - Управление формой
//
// Параметры:
//  Форма	 - УправляемаяФорма - 
//
&НаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	

КонецПроцедуры // УправлениеФормой(Форма)

&НаСервере
Процедура ОбновитьДанныеНаСервере()

	ОбщегоНазначенияСервер.ОбновитьВерсииТиповыхКонфигураций();
	
	//Заполнение версий типовый конфигураций
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииТиповыхСрезПоследних.Период КАК Период,
		|	ВерсииТиповыхСрезПоследних.Конфигурация КАК Конфигурация,
		|	ВерсииТиповыхСрезПоследних.Версия КАК Версия,
		|	МАКСИМУМ(ВерсииТиповыхКонфигураций.Период) КАК ДатаПредыдущейВерсии
		|ПОМЕСТИТЬ ВТ_ВерсииТиповыхКонфигураций
		|ИЗ
		|	РегистрСведений.ВерсииТиповыхКонфигураций КАК ВерсииТиповыхКонфигураций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВерсииТиповыхКонфигурацийСрезПоследних.Период КАК Период,
		|			ВерсииТиповыхКонфигурацийСрезПоследних.Конфигурация КАК Конфигурация,
		|			ВерсииТиповыхКонфигурацийСрезПоследних.Версия КАК Версия
		|		ИЗ
		|			РегистрСведений.ВерсииТиповыхКонфигураций.СрезПоследних КАК ВерсииТиповыхКонфигурацийСрезПоследних) КАК ВерсииТиповыхСрезПоследних
		|		ПО (ВерсииТиповыхСрезПоследних.Конфигурация = ВерсииТиповыхКонфигураций.Конфигурация)
		|			И (ВерсииТиповыхСрезПоследних.Период > ВерсииТиповыхКонфигураций.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВерсииТиповыхСрезПоследних.Период,
		|	ВерсииТиповыхСрезПоследних.Конфигурация,
		|	ВерсииТиповыхСрезПоследних.Версия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВерсииТиповыхКонфигураций.Период КАК ДатаВыходаПоследнегоРелиза,
		|	ВТ_ВерсииТиповыхКонфигураций.Конфигурация КАК Конфигурация,
		|	ВТ_ВерсииТиповыхКонфигураций.Версия КАК ТекущаяВерсия,
		|	ВТ_ВерсииТиповыхКонфигураций.ДатаПредыдущейВерсии КАК ДатаПредыдущейВерсии,
		|	ВерсииТиповыхКонфигураций.Версия КАК ПредыдущаяВерсия
		|ИЗ
		|	ВТ_ВерсииТиповыхКонфигураций КАК ВТ_ВерсииТиповыхКонфигураций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииТиповыхКонфигураций КАК ВерсииТиповыхКонфигураций
		|		ПО ВТ_ВерсииТиповыхКонфигураций.Конфигурация = ВерсииТиповыхКонфигураций.Конфигурация
		|			И ВТ_ВерсииТиповыхКонфигураций.ДатаПредыдущейВерсии = ВерсииТиповыхКонфигураций.Период";
	
	Объект.ВерсииКонфигураций.Загрузить(Запрос.Выполнить().Выгрузить());
	Объект.ВерсииКонфигураций.Сортировать("Конфигурация");
	
	//Запонение версий конфигураций информационных баз
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостояниеКонфигурацийИнформационныхБазСрезПоследних.Период КАК ДатаОбновления,
		|	СостояниеКонфигурацийИнформационныхБазСрезПоследних.Конфигурация КАК Конфигурация,
		|	СостояниеКонфигурацийИнформационныхБазСрезПоследних.ИнформационнаяБаза КАК ИнформационнаяБаза,
		|	СостояниеКонфигурацийИнформационныхБазСрезПоследних.Версия КАК УстановленнаяВерсия,
		|	ВерсииТиповыхКонфигурацийСрезПоследних.Версия КАК ТекущаяВерсияКонфигурации
		|ИЗ
		|	РегистрСведений.СостояниеКонфигурацийИнформационныхБаз.СрезПоследних КАК СостояниеКонфигурацийИнформационныхБазСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииТиповыхКонфигураций.СрезПоследних КАК ВерсииТиповыхКонфигурацийСрезПоследних
		|		ПО СостояниеКонфигурацийИнформационныхБазСрезПоследних.Конфигурация = ВерсииТиповыхКонфигурацийСрезПоследних.Конфигурация";
	
	Объект.ВерсииКонфигурацийИнформационныхБаз.Загрузить(Запрос.Выполнить().Выгрузить());
	Объект.ВерсииКонфигурацийИнформационныхБаз.Сортировать("Конфигурация, ИнформационнаяБаза");
	
КонецПроцедуры // ОбновитьДанныеНаСервере()

&НаСервереБезКонтекста
Функция ВыбратьРелизДляУстановки(ИнформационнаяБаза)

	НужнаяВерсия = "";
	Конфигурация = ИнформационнаяБаза.Конфигурация;
	//Получаем текущую версию конфигурации информационной базы
	УстановленнаяВерсия = РегистрыСведений.СостояниеКонфигурацийИнформационныхБаз.ПолучитьПоследнее(ТекущаяДата(), Новый Структура("Конфигурация, ИнформационнаяБаза", Конфигурация, ИнформационнаяБаза)).Версия;
	
	//Получаем актуальную версию конфигурации
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 10
		|	ВерсииТиповыхКонфигураций.Период КАК Период,
		|	ВерсииТиповыхКонфигураций.Конфигурация КАК Конфигурация,
		|	ВерсииТиповыхКонфигураций.Версия КАК Версия
		|ИЗ
		|	РегистрСведений.ВерсииТиповыхКонфигураций КАК ВерсииТиповыхКонфигураций
		|ГДЕ
		|	ВерсииТиповыхКонфигураций.Конфигурация = &Конфигурация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("Конфигурация", Конфигурация);
	ТаблицаВерсийКонфигураций = Запрос.Выполнить().Выгрузить();
	
	МассивВерсий = Новый Массив;
	Для каждого ВерсияКонфигурации Из ТаблицаВерсийКонфигураций Цикл
		
		Если ВерсияКонфигурации.Версия = УстановленнаяВерсия Тогда
			//Обновление не требуется
			Прервать;
		КонецЕсли;
		//Проверяем возможность обновления установленной версии до актуальной
		ВременныйФайл = "\\192.168.100.9\Exchange\Update_1c\1c\" + Конфигурация.НаименованиеКаталогаОбновлений + "\" + СтрЗаменить(ВерсияКонфигурации.Версия, ".", "_") + "\" + "UpdInfo.txt";
		
		СведенияОНовойВерсии = ОбщегоНазначенияСервер.ПрочитатьТекстовыйФайл(ВременныйФайл);
		Если НЕ СтрНайти(СведенияОНовойВерсии.FromVersions, УстановленнаяВерсия) = 0 Тогда
			//НужнаяВерсия = СведенияОНовойВерсии.Version;
			МассивВерсий.Добавить(СведенияОНовойВерсии.Version);
			Прервать;
		Иначе
			МассивВерсий.Добавить(СведенияОНовойВерсии.Version);
		КонецЕсли;
	КонецЦикла;

	Возврат МассивВерсий;
КонецФункции // ВыбратьРелизДляУстановки(ИнформационнаяБаза)

#КонецОбласти