

&НаКлиенте
Процедура ПолучитьСписокФизическихЛиц(Команда)
	ПолучитьСписокФизическихЛицНаСервере(ИнформационнаяБаза, СписокСотрудников);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьСписокФизическихЛицНаСервере(ИнформационнаяБаза, СписокСотрудников)
	Подключение = ПодключениеКлиентСервер.ВыполнитьПодключениеКБазе(ИнформационнаяБаза);
	
	Если Подключение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Подключение.NewObject("Запрос");
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Наименование КАК ФизическоеЛицоНаименование,
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицоСсылка
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних КАК КадроваяИсторияСотрудниковСрезПоследних
		|ГДЕ
		|	НЕ КадроваяИсторияСотрудниковСрезПоследних.ВидСобытия = Значение(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|
		|СГРУППИРОВАТЬ ПО
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Наименование,
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	КадроваяИсторияСотрудниковСрезПоследних.ФизическоеЛицо.Наименование";
	
	//Запрос.УстановитьПараметр("", );
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		GUID = Новый УникальныйИдентификатор(Подключение.XMLСтрока(ВыборкаДетальныеЗаписи.ФизическоеЛицоСсылка));
		СписокСотрудников.Добавить(GUID, ВыборкаДетальныеЗаписи.ФизическоеЛицоНаименование);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСотрудников(Команда)
	ЗагрузитьСотрудниковНаСервере(СписокСотрудников);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьСотрудниковНаСервере(СписокСотрудников)
	Для Каждого ЭлементСписка Из СписокСотрудников Цикл
		Если ЭлементСписка.Пометка Тогда
			СоздатьНовогоСотрудника(Новый УникальныйИдентификатор(ЭлементСписка.Значение), ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьНовогоСотрудника(Идентификатор, Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		НовыйСотрудник = Справочники.Сотрудники.СоздатьЭлемент();
		НовыйСотрудник.Идентификатор = Идентификатор;
		НовыйСотрудник.Наименование = Наименование;
		НовыйСотрудник.Записать();
	КонецЕсли;
КонецПроцедуры